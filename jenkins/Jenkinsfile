pipeline {
    agent {
        label 'terraform-aws'  // Usar agente específico con herramientas instaladas
    }
    
    environment {
        JOB_NAME = "terraform-aws"
        AWS_REGION = "us-east-1"
        AWS_CREDENTIALS_ID = 'aws-cli-token'
        ANSIBLE_DIR = "../ansible"
        TF_STATE_BUCKET = "tf-state-bucket-${params.ENVIRONMENT}"
        JENKINS_TRIGGER_TOKEN = "JENKINS_TRIGGER_TOKEN"
        SLACK_CHANNEL = '#infra-alerts'
        SLACK_TOKEN_CREDENTIAL_ID = 'slack-token'
        SLACK_TEAM_DOMAIN = 'aws-terraform-ansible'
    }

    parameters {
        string(name: 'GIT_SHA', defaultValue: '', description: 'Commit SHA from GitHub')
        string(name: 'GIT_REF', defaultValue: 'main', description: 'Git reference from GitHub')
        choice(name: 'ENVIRONMENT', choices: ['dev', 'prod'], description: 'Target environment')
    }

    triggers {
        GenericTrigger(
            genericVariables: [
                [
                    key: 'GIT_SHA', 
                    value: '$.sha',
                    expressionType: 'JSONPath',
                    defaultValue: ''
                ],
                [
                    key: 'GIT_REF', 
                    value: '$.ref',
                    expressionType: 'JSONPath',
                    defaultValue: 'main'
                ],
                [
                    key: 'ENVIRONMENT',
                    value: '$.environment',
                    expressionType: 'JSONPath',
                    regexpFilter: 'dev|prod',
                    defaultValue: 'dev'
                ]
            ],
            token: 'terraform-ci',
            printContributedVariables: true,
            silentResponse: false,
            printPostContent: true,
            causeString: 'Triggered by GitHub Actions',
        )
    }

    stages {
        stage('Download Terraform Plan') {
            when {
                expression { return params.GIT_SHA?.trim() } // Solo ejecuta si viene del webhook
            }
            steps {
                withCredentials([string(credentialsId: 'JENKINS_TRIGGER_TOKEN', variable: 'JENKINS_TRIGGER_TOKEN')]) {
                    script {
                        // 1. Obtener información del artifact
                        def artifactName = "tfplan-${params.GIT_SHA}-${params.ENVIRONMENT}"
                        echo "Buscando artifact con nombre: ${artifactName}"
                        
                        def artifactsResponse = sh(returnStdout: true, script: """
                            curl -sL https://api.github.com/repos/alexhl005/aws-terraform-ansible/actions/artifacts
                        """).trim()
                        
                        // Procesar la respuesta para encontrar el artifact específico
                        def artifactJson = readJSON text: artifactsResponse
                        def foundArtifact = artifactJson.artifacts.find { it.name == artifactName }
                        
                        if (!foundArtifact) {
                            error "No se encontró el artifact con nombre: ${artifactName}"
                        }
                        
                        def artifactId = foundArtifact.id
                        def workflowRunId = foundArtifact.workflow_run.id
                        def downloadUrl = foundArtifact.archive_download_url
                        
                        echo "Artifact encontrado - ID: ${artifactId}, Workflow Run ID: ${workflowRunId}"
                        echo "URL de descarga: ${downloadUrl}"
                        
                        // 2. Descargar el artifact
                        sh """
                            curl -sL -H "Authorization: token \$JENKINS_TRIGGER_TOKEN" \\
                                -o tfplan.zip \\
                                "${downloadUrl}"
                            
                            unzip -o tfplan.zip -d "terraform/environments/${params.ENVIRONMENT}/"
                            ls -la "terraform/environments/${params.ENVIRONMENT}/"
                            rm -f tfplan.zip
                        """
                    }
                }
            }
        }

        stage('Terraform Apply') {
            steps {
                script {
                    def userInput = input(
                        id: 'confirm', 
                        message: "¿Aplicar los cambios de Terraform en ${params.ENVIRONMENT}?", 
                        parameters: [
                            text(name: 'reason', description: 'Razón del despliegue', defaultValue: '')
                        ])
                    
                    withCredentials([[
                        $class: 'AmazonWebServicesCredentialsBinding',
                        credentialsId: env.AWS_CREDENTIALS_ID,
                        accessKeyVariable: 'AWS_ACCESS_KEY_ID',
                        secretKeyVariable: 'AWS_SECRET_ACCESS_KEY'
                    ]]) {
                        dir("terraform/environments/${params.ENVIRONMENT}") {
                            sh 'terraform init -reconfigure'
                            sh 'terraform apply -input=false -auto-approve plan.tfplan'
                        }
                    }
                }
            }
        }

        stage('Generate Terraform Outputs') {
            steps {
                dir("terraform/environments/${params.ENVIRONMENT}") {
                    sh """
                    terraform output -json > ../../tf_outputs/tf_outputs_${params.ENVIRONMENT}.json
                    """
                }
            }
        }

        stage('Configure Ansible Inventory') {
            steps {
                script {
                    def tfOutputs = readJSON file: "tf_outputs/tf_outputs_${params.ENVIRONMENT}.json"
                    
                    writeFile file: "${env.ANSIBLE_DIR}/inventories/${params.ENVIRONMENT}/hosts", 
                                text: """
                    [webservers]
                    ${tfOutputs.ec2_instances.value.join('\n')}

                    [webservers:vars]
                    ansible_user=ubuntu
                    ansible_ssh_private_key_file=../modules/ec2/.ssh/dev-wp-key.pem
                    db_host=${tfOutputs.rds_endpoint.value}
                    elb_dns=${tfOutputs.elb_dns_name.value}
                    """
                }
            }
        }

        stage('Update Configuration Files') {
            steps {
                script {
                    // Leer los outputs de Terraform
                    def tfOutputs = readJSON file: "tf_outputs/tf_outputs_${params.ENVIRONMENT}.json"
                    
                    // Actualizar el archivo PHP con el cluster endpoint
                    def phpConfigFile = "${env.ANSIBLE_DIR}/roles/wordpress/templates/wp-config.php.j2"
                    def phpContent = readFile(file: phpConfigFile)
                    phpContent = phpContent.replace("(cluster_endpoint)", tfOutputs.cluster_endpoint.value)
                    writeFile(file: phpConfigFile, text: phpContent)
                    
                    // Actualizar el script bash con el bucket ARN
                    def bashScriptFile = "../scripts/bash/backup/s3_sync.sh"
                    def bashContent = readFile(file: bashScriptFile)
                    bashContent = bashContent.replace("(bucket_arn)", tfOutputs.bucket_arn.value)
                    writeFile(file: bashScriptFile, text: bashContent)
                    
                    // Confirmar los cambios (opcional)
                    sh """
                        echo "Archivos actualizados:"
                        echo " - ${phpConfigFile}: DB_HOST ahora es ${tfOutputs.cluster_endpoint.value}"
                        echo " - ${bashScriptFile}: S3_BUCKET ahora es ${tfOutputs.bucket_arn.value}"
                    """
                }
            }
        }

        stage('Ansible Deployment') {
            steps {
                dir(env.ANSIBLE_DIR) {
                    withCredentials([file(credentialsId: "AWS_SSH_KEY_${params.ENVIRONMENT}", variable: 'SSH_KEY')]) {
                        sh """
                        mkdir -p /opt/keys
                        cp ${SSH_KEY} /opt/keys/aws-key-${params.ENVIRONMENT}.pem
                        chmod 600 /opt/keys/aws-key-${params.ENVIRONMENT}.pem
                        
                        ansible-playbook -i inventories/${params.ENVIRONMENT}/hosts playbooks/wordpress.yml \\
                            -e "@../tf_outputs/tf_outputs_${params.ENVIRONMENT}.json" \\
                            -e "env=${params.ENVIRONMENT}"
                        """
                    }
                }
            }
        }
    }

    post {
        always {
            archiveArtifacts artifacts: "tf_outputs/tf_outputs_${params.ENVIRONMENT}.json"
            cleanWs()
        }
        failure {
            script {
                slackSend channel: '#infra-alerts',
                         color: 'danger',
                         tokenCredentialId: env.SLACK_TOKEN_CREDENTIAL_ID,
                         message: """Pipeline fallido: ${env.JOB_NAME} #${env.BUILD_NUMBER}
Environment: ${params.ENVIRONMENT}
Commit: ${params.GIT_SHA}
Build URL: ${env.BUILD_URL}"""
            }
        }
        success {
            script {
                slackSend channel: '#infra-alerts',
                         color: 'good',
                         tokenCredentialId: env.SLACK_TOKEN_CREDENTIAL_ID,
                         message: """Despliegue exitoso: ${env.JOB_NAME} #${env.BUILD_NUMBER}
Environment: ${params.ENVIRONMENT}
Commit: ${params.GIT_SHA}"""
            }
        }
    }
}